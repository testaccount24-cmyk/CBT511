PROC 1 DSN

CONTROL ASIS
ISPEXEC CONTROL ERRORS RETURN             /*                         */

/*                                                                   */
/* CLIST NAME: GENSUB                                                */
/*                                                                   */
/* This CLIST is a generalized SUBMIT CLIST which displays ISPF      */
/* panel GENSUB and allows the user to cancel the SUBMIT; do the     */
/* SUBMIT; edit the job prior to SUBMIT; or browse the job prior     */
/* to SUBMIT.                                                        */

CONTROL PROMPT

SET RC EQ 0

SET &DSNX = &DSN
ISPEXEC VPUT DSNX
ISPEXEC SELECT PGM(FIXUPDSN) PARM(DSNX)     /* SHARE tape program   */
ISPEXEC VGET DSNX
SET &DSNX = &DSNX  /* REMOVE TRAILING BLANKS PER ISPF/PDF V2 */

DISPLAY: +
ISPEXEC DISPLAY PANEL(GENSUB)
SET RC EQ &LASTCC
IF &RC NE 0 THEN GOTO ENDIT

IF &SOPT = END THEN DO
  SET RC EQ 8
  GOTO ENDIT
  END

IF &SOPT = C THEN DO
  SET RC EQ 12
  GOTO ENDIT
  END

IF &SOPT = S THEN DO
                    SUBMIT '&DSNX'
                    GOTO ENDIT
                    END

IF &SOPT = E THEN DO
  ISPEXEC EDIT DATASET('&DSNX')
  IF &LASTCC EQ 0 THEN ISPEXEC SETMSG MSG(ISRU244)
  END

IF &SOPT = B THEN ISPEXEC BROWSE DATASET('&DSNX')

GOTO DISPLAY    /* ASK THE QUESTION AGAIN */

ENDIT: +
EXIT CODE(&RC)
